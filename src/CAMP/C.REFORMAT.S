
LINENUM			=	$00

				DUMMY SCRATCH_PAGE
POSITS			DS	7
POSITS2			DS	7
CORNUMB			DS	7
CORNUM2			DS	7
				DEND

SORTER			LDX	GRPNUMB
MORCOR			DEX
				TXA
				STA	CORNUMB,X
				BNE	MORCOR
				JSR	POSILOD
				LDX	GRPNUMB
COPOS			DEX
				LDA	POSITS,X
				STA	POSITS2,X
				CPX	#$00
				BNE	COPOS
				LDX	GRPNUMB
				DEX					;Only sort if more than
				BEQ	SORTED			; one character in the group
NOTDONE			LDA	#$00
				STA	rSWFLAG
				LDX	#$00
				DEC	GRPNUMB
KEEPGON			INC	GRPNUMB
				LDA	POSITS2,X
				PHA
				AND	#$0F
				STA	rSCRACH2
				INX
				LDA	POSITS2,X
				AND	#$0F
				CMP	rSCRACH2
				BEQ	SAMHIGH
				PLA
				BCC	SORTING
SWITCHN			LDY	POSITS2,X
				LDA	POSITS2-1,X
				STA	POSITS2,X
				TYA
				STA	POSITS2-1,X
				LDY	CORNUMB,X
				LDA	CORNUMB-1,X
				STA	CORNUMB,X
				TYA
				STA	CORNUMB-1,X
				LDA	#$FF
				STA	rSWFLAG
				BNE	SORTING
SAMHIGH			PLA
				AND	#$F0
				STA	rSCRACH2
				LDA	POSITS2,X
				AND	#$F0
				CMP	rSCRACH2
				BCC	SWITCHN
SORTING			DEC	GRPNUMB
				CPX	GRPNUMB
				BNE	KEEPGON
				INC	GRPNUMB
				LDA	rSWFLAG
				BNE	NOTDONE
SORTED			JSR	POSILOD
				LDX	GRPNUMB
MORCOR2			DEX
				TXA
				STA	CORNUM2,X
				BNE	MORCOR2
				LDA	#$00
				STA	COUNT
GOFLIP			LDY	COUNT
				LDA	CORNUMB,Y
				LDY	GRPNUMB
				DEY
FINDCOR			CMP	CORNUM2,Y
				BEQ	GOTHE1
				DEY
				BPL	FINDCOR
GOTHE1			CPY	COUNT
				BEQ	NOFLIP
				LDX	COUNT
				LDA	CORNUM2,X
				PHA
				LDA	CORNUM2,Y
				STA	CORNUM2,X
				PLA
				STA	CORNUM2,Y
				JSR	FLPDATA
NOFLIP			INC	COUNT
				LDA	COUNT
				CMP	GRPNUMB
				BNE	GOFLIP
				RTS

FLPDATA			TXA
				PHA
				TYA
				JSR	SET_CHAR
				LDA	CHARDL
				STA	DESTINL
				LDA	CHARDH
				STA	DESTINH
				PLA
				JSR	SET_CHAR
				LDY	#$7F
:1				LDA	(CHARDL),Y
				TAX
				LDA	(DESTINL),Y
				STA	(CHARDL),Y
				TXA
				STA	(DESTINL),Y
				DEY
				BPL	:1
				RTS

POSILOD			JSR FIRST_CHAR
:1				LDY	#position
				LDA	(CHARDL),Y
				PHA
				LDA	POSITS,X
				STA	(CHARDL),Y
				PLA
				STA	POSITS,X
				JSR NEXT_CHAR
				BCC :1
				RTS

DRAW_SEL_MEMB	JSR DRAW_PICT
				PictClear 15;86;37;93
				PictTextAt 108;86;(SELECT_MEMBER_TO_BE_MOVED)
				PictClear 22;170;29;177
				PictTextAt 162;170;(L>EAVE)
				PictEnd
				RTS

DRAW_SEL_DEST	JSR DRAW_PICT
				PictClear 15;86;37;93
				PictTextAt 126;86;(SELECT_DESTINATION)
				PictClear 22;170;29;177
				PictTextAt 160;170;(C>ANCEL)
				PictEnd
				RTS

REFORMAT		JSR	DRAW_PICT
				PictSetPage2
				PictClearPage
				PictSetGreen
				PictRect $59;$47;$115;$BF
				PictSetViolet
				PictMoveTo $B6;$70
				PictLineTo $B6;$84
				PictMoveTo $AA;$7A
				PictLineTo $C2;$7A
				PictTextAt 132;152;(<SPACE>_TO_ENTER)
				PictTextAt 125;119;(J_OR)
				PictTextAt 217;119;(OR_K)
				PictTextAt 180;103;(A)
				PictTextAt 180;135;(Z)
				; PictTextAt 148;161;(F>LIP_STATS)
				PictEnd

				JSR DRAW_SEL_MEMB

				LDX #198
				LDY #119
				JSR DRAW_ARROW

				LDX #155
				LDY #119
				JSR DRAW_BACK_ARROW

				JSR	PICPOSI
				JSR	SORTER
				JSR	DRAW_GRID

				; JSR DRAW_STATS_BOX_XY
				JSR DRAW_STATS_BOX

MLOOP			JSR	POSILOD
				LDA	POSITS
				STA	CURSOR
				LDA	#$00
				STA	rARRCURS
				JSR	ARROW
				JSR	CURSIT1
				BIT	UNSTROB
				STA	SCNDARY
				JMP	MOVKEY
BLINK			LDA	#$10
				JSR	WAIT
				LDA	#$F0
				JSR	WAIT_KEY
				BMI	GOTAKEY
				JSR	CURSIT1
				JSR	ARROW
				LDA	#$00
				JSR	WAIT_KEY
				JSR	CURSIT1
				JSR	ARROW
MOVKEY			LDA	KEYBRD
				BPL	BLINK
GOTAKEY			BIT	UNSTROB
				JSR	CHECK_UP
				BEQ	YLEFT1
				JSR	CHECK_LEFT
				BNE	NOLEFTS
YLEFT1			JMP	LEFTONE
NOLEFTS			JSR	CHECK_RIGHT
				BEQ	YRIGHT1
				JSR	CHECK_DOWN
				BNE	NORGHTS
YRIGHT1			JMP	RFRIGHT1
NORGHTS			CMP	#"L"
				BEQ	LEAFIT
				JSR	CHECK_ENTER
				BNE	NOTSPAC
				JMP	SPACEIT
NOTSPAC
				; CMP	#"F"
				; BNE	MOVKEY
				; JMP	FLPSTAT
LEAFIT			LDA	CURSOR
				PHA
				LDA	#$15
				STA	CURSOR
				JSR	SCANLIN
				CMP	#$FF
				BNE NOGOE

CANGOE			JSR	ARROW
				JSR	POSILOD
				PLA
				JSR	DRAW_GRID_CURSOR
				JMP	RECAMP

NOGOE			JSR	DRAW_PICT
				PictClear 15;161;38;185
				PictTextAt 106;170;(THERE_MUST_BE_AT_LEAST_ONE)
				PictTextAt 112;178;(PERSON_IN_THE_FRONT_ROW)
				PictEnd
				LDY	#$05
MORTIME			LDA	#$00
				JSR	WAIT_KEY
				BMI	LESTIME
				DEY
				BPL	MORTIME
LESTIME			BIT	UNSTROB
				JSR	DRAW_PICT
				PictClear 15;170;38;185
				; PictTextAt 148;161;(F>LIP_STATS)
				PictTextAt 162;170;(L>EAVE)
				PictEnd
				PLA
				STA	CURSOR
				JMP	MOVKEY

; FLPSTAT		JSR	POSILOD
; 				JSR	EXFLIP
; 				JSR	POSILOD
; 				JMP	MOVKEY

LEFTONE			LDA	CURSOR
				PHA
GOGOL			JSR	GOLEFT
				JSR	LOOKFOR
				CMP	#$FF
				BNE	GOGOL
				BEQ	COMUDLR
RFRIGHT1		LDA	CURSOR
				PHA
GOGOR			JSR	GORGHT
				JSR	LOOKFOR
				CMP	#$FF
				BNE	GOGOR
COMUDLR			PLA
				JSR	DRAW_GRID_CURSOR
				JSR	CURSIT1
				JSR	ARROW
				LDA	CURSOR
				JSR	LOOKFOR
				STX	rARRCURS
				JSR	ARROW
				JMP	MOVKEY

; EXFLIP          JSR DRAW_PICT
;                 PictVMoveTo 2
;                 PictEnd
; 				JMP FLIP_STATS

SPACEIT			JSR	ARROW
				JSR	MARKER
				JSR DRAW_SEL_DEST
				LDA	CURSOR
				STA	rSOURCE
				AND	#$F0
				CMP	#$50
				BEQ	ATLEFT
				LDA	CURSOR
				CLC
				ADC	#$10
				STA	CURSOR
				BNE	GOCURSE
ATLEFT			LDA	CURSOR
				SEC
				SBC	#$10
				STA	CURSOR
GOCURSE			JSR	DRAW_GRID_CURSOR
				LDA	CURSOR
				JSR	LOOKFOR
				STA	rFLPFLAG
				BIT	UNSTROB
				JMP	MOVKEY2
BLINK2			LDA	#$10
				JSR	WAIT
				LDA	#$F0
				JSR	WAIT_KEY
				BMI	GOTKEY2
				JSR	CURSIT1
				LDA	#$00
				JSR	WAIT_KEY
				JSR	CURSIT1
MOVKEY2			LDA	KEYBRD
				BPL	BLINK2
GOTKEY2			BIT	UNSTROB
				JSR	CHECK_LEFT
				BEQ	LEFT2
				JSR	CHECK_RIGHT
				BEQ	RIGHT2
				JSR	CHECK_UP
				BEQ	RFUP2
				JSR	CHECK_DOWN
				BEQ	RFDOWN2
				CMP	#"C"
				BEQ	CANCELD
				JSR	CHECK_ENTER
				; BNE	:1
				BNE	MOVKEY2
				JMP SPACIT2
; :1			CMP	#"F"
; 				BNE	MOVKEY2
; 				JSR	POSILOD
; 				JSR	EXFLIP
; 				JSR	POSILOD
; 				JMP	MOVKEY2

LEFT2			LDA	CURSOR
				PHA
NOTLET1			JSR	GOLEFT2
				CMP	rSOURCE
				BEQ	NOTLET1
				BNE	COMUD2
RIGHT2			LDA	CURSOR
				PHA
NOTLET2			JSR	GORGHT2
				CMP	rSOURCE
				BEQ	NOTLET2
				BNE	COMUD2
RFUP2			LDA	CURSOR
				PHA
NOTLET3			JSR	GOUP2
				CMP	rSOURCE
				BEQ	NOTLET3
				BNE	COMUD2
RFDOWN2			LDA	CURSOR
				PHA
NOTLET4			JSR	GODOWN2
				CMP	rSOURCE
				BEQ	NOTLET4
COMUD2			JSR	LOOKFOR
				STA	rFLPFLAG
				PLA
				JSR	DRAW_GRID_CURSOR
				JSR	CURSIT1
				JMP	MOVKEY2
CANCELD			JSR	CURSIT1
				LDA	rSOURCE
				STA	CURSOR
				JSR	MARKER
				JSR	ARROW
				JSR DRAW_SEL_MEMB
				JMP	MOVKEY

SPACIT2			JSR	MARKER
				LDA	rSOURCE
				JSR	DRAW_GRID_CURSOR
				JSR	CURSIT1
				BIT	SELECT
				BPL	NOTXY1
				JSR	XYSOURC
NOTXY1			LDA	CURSOR
				JSR	LOOKFOR
				STX	rYHOLD1
				LDA	rSOURCE
				JSR	LOOKFOR
				STX	XPNT
				LDA	rFLPFLAG
				BPL	ONLY1IN
				BIT	SELECT
				BPL	NOTXY2
				JSR	XYCURSR
NOTXY2			LDX	XPNT
				LDY	rYHOLD1
				LDA	POSITS,X
				PHA
				LDA	POSITS,Y
				STA	POSITS,X
				PLA
				STA	POSITS,Y
				JSR	POSILOD
				JSR	FLPVIEW
				LDX	XPNT
				LDY	rYHOLD1
				JSR	FLPDATA
				LDX	XPNT
				LDY	rYHOLD1
				JSR	FLIPLIN
				JMP	BACK2IT

ONLY1IN			LDX	XPNT
				LDA	CURSOR
				STA	POSITS,X
				JSR	POSILOD
				LDA	XPNT
				JSR	POINTIT
				LDY	#profrace
				LDA	(CHARDL),Y
				LSR
				LSR
				LSR
				LSR
				PHA
				LDA	rSOURCE
				JSR	POS_TO_XY
				PLA
				PHA
				JSR	DRAW_TOPVIEW
				LDA	CURSOR
				JSR	POS_TO_XY
				PLA
				JSR	DRAW_TOPVIEW
				JSR	SORTER
				LDX	#$00
:LOOP1			STX	COUNT
				LDA	CORNUM2,X
				CMP	COUNT
				BNE	OUTORD
				INX
				CPX	GRPNUMB
				BNE	:LOOP1
				JMP	BACK2IT

OUTORD			TXA
				PHA
				BPL	OLOOP
MORORD2			STX	COUNT
				LDA	CORNUM2,X
				CMP	COUNT
				BEQ	GOTEND1
OLOOP			INX
				CPX	GRPNUMB
				BNE	MORORD2
GOTEND1			DEX
				TXA
				TAY
				PLA
				TAX
				LDA	rSOURCE
				AND	#$0F
				STA	rYHOLD1
				LDA	CURSOR
				AND	#$0F
				CMP	rYHOLD1
				BCC	CLUMPU
				BNE	CLUMPD
				LDA	rSOURCE
				AND	#$F0
				STA	rYHOLD1
				LDA	CURSOR
				AND	#$F0
				CMP	rYHOLD1
				BCC	CLUMPD
CLUMPU			JSR	MCLUMP2
				JMP	BACK2IT
CLUMPD			JSR	MCLUMP
				JMP	BACK2IT

MCLUMP			LDA	#$07
				STA	COUNT
				LDA	YPOINTS,X
				STA	rYHOLD1
				LDA	YPOINTS,Y
				CLC
				ADC	#$07
				STA	rYHOLD2
GOMOVE1			LDA	rYHOLD2
				STA	LINENUM
				JSR	MOVER1
MOVER2			LDY	LINENUM
				JSR	RSUBB
				DEY
				JSR	RSUBA
				JSR	LINMOVE
				DEC	LINENUM
				LDA	LINENUM
				CMP	rYHOLD1
				BNE	MOVER2
				LDY	rYHOLD1
				JSR	RSUBB
				JSR	RSUBA2
				JSR	LINMOVE
				DEC	COUNT
				BMI	CLOUT
				JMP	GOMOVE1
CLOUT			RTS

MCLUMP2			LDA	#$07
				STA	COUNT
				LDA	YPOINTS,X
				STA	rYHOLD2
				LDA	YPOINTS,Y
				CLC
				ADC	#$07
				STA	rYHOLD1
GOMOVE2			LDA	rYHOLD2
				STA	LINENUM
				JSR	MOVER1
MOVER2B			LDY	LINENUM
				JSR	RSUBB
				INY
				JSR	RSUBA
				JSR	LINMOVE
				INC	LINENUM
				LDA	LINENUM
				CMP	rYHOLD1
				BNE	MOVER2B
				LDY	rYHOLD1
				JSR	RSUBB
				JSR	RSUBA2
				JSR	LINMOVE
				DEC	COUNT
				BPL	GOMOVE2
				RTS

MOVER1			LDY	rYHOLD2
				JSR	RSUBA
				JSR	RSUBB2
				; fall through

LINMOVE			LDY	#$27
:1				LDA	(SCREENL),Y
				STA	(DESTINL),Y
				DEY
				BPL	:1
				RTS

BACK2IT			JSR DRAW_SEL_MEMB
				JMP	MLOOP

XYSOURC			LDA	rSOURCE
				JSR	LOOKFOR
				LDY	YPOINTS,X
				JSR VMOVE_TO
				LDA	rSOURCE
				JSR XYOUT
				LDA	CURSOR
				JMP XYOUT

XYCURSR			LDA	CURSOR
				JSR	LOOKFOR
				LDY	YPOINTS,X
				JSR VMOVE_TO
				LDA	CURSOR
				JSR XYOUT
				LDA	rSOURCE
				; fall through

XYOUT			JSR	POS_TO_TEXT
				LDX	#$71
				JMP	DRAW_TEXTBUF_X

FLPVIEW			LDA	XPNT
				JSR	POINTIT
				LDY	#profrace
				LDA	(CHARDL),Y
				PHA
				LDA	rSOURCE
				JSR	POS_TO_XY
				PLA
				LSR
				LSR
				LSR
				LSR
				PHA
				JSR	DRAW_TOPVIEW
				LDA	rYHOLD1
				JSR	POINTIT
				LDY	#profrace
				LDA	(CHARDL),Y
				PHA
				LDA	rSOURCE
				JSR	POS_TO_XY
				PLA
				LSR
				LSR
				LSR
				LSR
				PHA
				JSR	DRAW_TOPVIEW
				LDA	CURSOR
				JSR	POS_TO_XY
				PLA
				JSR	DRAW_TOPVIEW
				LDA	CURSOR
				JSR	POS_TO_XY
				PLA
				JMP	DRAW_TOPVIEW

FLIPLIN			LDA	#$06
				STA	COUNT
				LDA	YPOINTS,X
				STA	rYHOLD1
				PHA
				LDA	YPOINTS,Y
				STA	rYHOLD2
				PHA
ONEOF7			JSR	RSUBB2
				LDA	rYHOLD2
				CLC
				ADC	#$06
				TAY
				JSR	RSUBA
				JSR	LINMOVE
				LDA	rYHOLD2
				CLC
				ADC	#$05
				STA	rYSORC
				STA	rYDEST
				INC	rYDEST
TRANLOP			LDY	rYSORC
				JSR	RSUBA
				LDY	rYDEST
				JSR	RSUBB
				JSR	LINMOVE
				LDA	rYSORC
				CMP	rYHOLD2
				BNE	NOTGAP1
				LDA	rYHOLD1
				CLC
				ADC	#$06
				STA	rYSORC
				BNE	NOTGAP2
NOTGAP1			CMP	rYHOLD1
				BEQ	BACKFRM
				DEC	rYSORC
				LDA	rYDEST
				CMP	rYHOLD2
				BNE	NOTGAP2
				LDA	rYHOLD1
				CLC
				ADC	#$06
				STA	rYDEST
				BNE	TRANLOP
NOTGAP2			DEC	rYDEST
				JMP	TRANLOP
BACKFRM			JSR	RSUBA2
				LDY	rYHOLD1
				JSR	RSUBB
				JSR	LINMOVE
				PLA
				STA	rYHOLD2
				PLA
				PHA
				STA	rYHOLD1
				LDA	rYHOLD2
				PHA
				DEC	COUNT
				BMI	FLIPPED
				JMP	ONEOF7
FLIPPED			PLA
				PLA
				RTS

GOLEFT			LDA	CURSOR
				AND	#$F0
				CMP	#$10
				BNE	LEFT1A
				LDA	CURSOR
				AND	#$0F
				CMP	#$05
				BNE	LEFT1B
				LDA	#$51
				STA	CURSOR
				BNE	LEFTOUT
LEFT1A			LDA	CURSOR
				SEC
				SBC	#$10
				STA	CURSOR
				BCS	LEFTOUT
LEFT1B			CLC
				ADC	#$01
				ORA	#$50
				STA	CURSOR
LEFTOUT			RTS

GORGHT			LDA	CURSOR
				AND	#$F0
				CMP	#$50
				BNE	RIGHT1A
				LDA	CURSOR
				AND	#$0F
				CMP	#$01
				BNE	RIGHT1B
				LDA	#$15
				STA	CURSOR
				BNE	RGHTOUT
RIGHT1A			LDA	CURSOR
				CLC
				ADC	#$10
				STA	CURSOR
				BNE	RGHTOUT
RIGHT1B			SEC
				SBC	#$01
				ORA	#$10
				STA	CURSOR
RGHTOUT			RTS

GOLEFT2			LDA	CURSOR
				AND	#$F0
				CMP	#$10
				BNE	NLEDGE
				LDA	CURSOR
				AND	#$0F
				ORA	#$50
				STA	CURSOR
				BNE	LOUT2
NLEDGE			LDA	CURSOR
				SEC
				SBC	#$10
				STA	CURSOR
LOUT2			RTS

GORGHT2			LDA	CURSOR
				AND	#$F0
				CMP	#$50
				BNE	NREDGE
				LDA	CURSOR
				AND	#$0F
				ORA	#$10
				STA	CURSOR
				BNE	ROUT2
NREDGE			LDA	CURSOR
				CLC
				ADC	#$10
				STA	CURSOR
ROUT2			RTS

GOUP2			LDA	CURSOR
				AND	#$0F
				CMP	#$05
				BNE	NOTOPD2
				LDA	CURSOR
				AND	#$F0
				ORA	#$01
				STA	CURSOR
				BNE	GOUPOUT
NOTOPD2			LDA	CURSOR
				CLC
				ADC	#$01
				STA	CURSOR
GOUPOUT			RTS

GODOWN2			LDA	CURSOR
				AND	#$0F
				CMP	#$01
				BNE	NOTBOT2
				LDA	CURSOR
				AND	#$F0
				ORA	#$05
				STA	CURSOR
				BNE	GODOUT
NOTBOT2			LDA	CURSOR
				SEC
				SBC	#$01
				STA	CURSOR
GODOUT			RTS

LOOKFOR			LDX	GRPNUMB
:LOOP1			DEX
				CMP	POSITS,X
				BEQ	:SKIPA
				CPX	#$00
				BNE	:LOOP1
				RTS
:SKIPA			LDA	#$FF
				RTS

SCANLIN			LDA	CURSOR
				JSR	LOOKFOR
				CMP	#$FF
				BEQ	SCANOUT
				LDA	CURSOR
				AND	#$F0
				CMP	#$50
				BEQ	NONHERE
				LDA	CURSOR
				CLC
				ADC	#$10
				STA	CURSOR
				BNE	SCANLIN
NONHERE			LDA	#$00
SCANOUT			RTS

RSUBA2			LDY	#$00
				JSR	RSUBA
				LDA	SCREENH
				EOR	#$60
				STA	SCREENH
				RTS

RSUBB2			LDY	#$00
				JSR	RSUBB
				LDA	DESTINH
				EOR	#$60
				STA	DESTINH
				RTS

RSUBA			LDA	LOBYTES,Y
				STA	SCREENL
				LDA	HIBYTES,Y
				STA	SCREENH
				RTS

RSUBB			LDA	LOBYTES,Y
				STA	DESTINL
				LDA	HIBYTES,Y
				STA	DESTINH
				RTS

CURSIT1			LDA	CURSOR
				JMP	DRAW_GRID_CURSOR

MARKER     		LDX	rARRCURS
				LDY YPOINTS,X
                LDX #$06
                JMP DRAW_MARKER

ARROW			LDX	rARRCURS
				LDY	YPOINTS,X
				LDX	#$03
				JMP	DRAW_ARROW

PICPOSI			JSR	POSILOD
				LDY	#0
				STY	COUNT
PICANEW			LDA	POSITS,Y
				BNE	NOZERO
				JSR	NEWPICR
NOZERO			INC	COUNT
				LDY	COUNT
				CPY	GRPNUMB
				BNE	PICANEW
				CPY	#1
				BEQ	PICEXIT
ALLOVER			LDA	#0
				STA	rSTART
COMLOP2			LDY	rSTART
				LDA	POSITS,Y
				INY
COMLOOP			CMP	POSITS,Y
				BNE	RNOSAME2
				JSR	NEWPICR
				JMP	ALLOVER
RNOSAME2		INY
				CPY	GRPNUMB
				BNE	COMLOOP
				INC	rSTART
				LDA	GRPNUMB
				SEC
				SBC	#$01
				CMP	rSTART
				BNE	COMLOP2
PICEXIT			JMP	POSILOD

NEWPICR			JSR	RANDOM
				AND	#$33
				CLC
				ADC	#$22
				STA	POSITS,Y
				RTS
