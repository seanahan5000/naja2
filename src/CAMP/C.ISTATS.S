
; valid for all I.XXX.S source files
ISOURCE_CHAR	=	$C0
ITARGET_CHAR	=	$C1

DOSTATS			LDA	CURSOR
				STA	ISOURCE_CHAR

RESTATS			JSR DRAW_PICT
				PictSetPage2
				PictClearPage
				PictSetGreen
				PictRect $A5;$00;$115;$BF
				PictEnd

				JSR IS_ROBOT
				BCS :ROBOT

:CHARACTER		JSR DRAW_PICT
				PictImage CHAR_IMAGE
				PictEnd
				JSR	DRAW_CHAR_EQUIP
				JSR	DRAW_CHAR_INFO
				JMP	:COMMON

:ROBOT			JSR DRAW_PICT
				PictImage ROBOT_IMAGE
				PictEnd
				JSR	DRAW_ROBO_EQUIP
				JSR	DRAW_ROBO_INFO

:COMMON			JSR DRAW_PICT
				PictTextAt 171;116;(EQUIPMENT_OPTIONS)
				PictTextAt 198;125;(MOVE\nDROP\nUSE\nEXAMINE)
				PictTextAt 180;162;(TRANSFER_ENERGY\nSEE_STATISTICS)
				PictTextAt 180;181;(LEAVE)
				PictShowPage
				PictEnd

; TODO: draw arrow instructions

				LDA	#0
				STA	ICURPOS
IMENU			LDX	#<ISTAT_MENU
				LDY	#>ISTAT_MENU
				JSR	MENU_INIT
				LDX #<ISTAT_FILTER
				LDY #>ISTAT_FILTER
				JSR MENU_SET_FILTER
				JSR MENU_SELECT
				STX ICURPOS
				JSR	IMARKER
				JSR MENU_TARGET
				LDX ICURPOS
				JSR	IMARKER
				LDA ISOURCE_CHAR
				JSR SET_CHAR
				JMP IMENU

*-------------------------------
ISTAT_MENU		DB	$87
ICURPOS			DB	$00
				DW	:ISTAT_CURS
				DW	:ISTAT_JUMPS

:ISTAT_JUMPS	DW	TO_MOVER
				DW	TO_DROPPER
				DW	TO_USE
				DW	TO_EXAMINE
				DW	TO_TRANSFER
				DW	SEESTAT
				DW	TO_RECAMP

:ISTAT_CURS		JSR	XY_SUB
				JMP	DRAW_ARROW
*-------------------------------

ISTAT_FILTER	LDA KEYBRD
				JSR CHECK_LEFT
				CLC
				BEQ :LEFT_RIGHT
				JSR CHECK_RIGHT
				SEC
				BEQ :LEFT_RIGHT
;				SEC						; set to continue
				RTS

:LEFT_RIGHT		PLA
				PLA
				BIT UNSTROB
				PHP
				JSR MENU_DRAW_CURSOR
				PLP
				BCS :1
				JSR DO_PREV_CHAR
				JMP IMENU
:1				JSR DO_NEXT_CHAR
				JMP IMENU

*-------------------------------

TO_MOVER		JSR BANK_2
				JMP MOVESUB

TO_DROPPER		JSR BANK_2
				JMP DROPSUB

TO_RECAMP		PLA
				PLA
				JMP RECAMP

TO_USE          LDA SHELL
                BNE :CAN_USE
				JSR DRAW_PICT
				PictSetGreen
				PictMoveTo $AB;$32
				PictLineTo $10F;$32
				PictMoveTo $AB;$67
				PictLineTo $10F;$67
		        PictTextAt 177;63;(NOT_ALLOWED_IN)
                PictTextAt 189;72;(MOTHERSHIP)
                PictTextAt 198;84;(<SPACE>)
				PictEnd
                BIT UNSTROB
:KEYLOOP        LDA KEYBRD
                BPL :KEYLOOP
                BIT UNSTROB
                JSR CHECK_ENTER
                BNE :KEYLOOP
                JSR DRAW_PICT
                PictClear 24;50;39;104
                PictEnd
:GO_BACK        RTS

:CAN_USE        LDY #status
                LDA (CHARDL),Y
                CMP #dead
                BEQ :GO_BACK
				JSR BANK_2
				JMP USER

TO_EXAMINE		JSR BANK_2
				JMP EXAMINE

TO_TRANSFER		LDY	#profrace
				LDA	(CHARDL),Y
				AND	#$F0
				CMP	#juicer*16
				BNE	:NOJUICER
				JMP	TRANSFER
:NOJUICER		RTS

; On entry:
;	X: CURSOR
;
IMARKER			JSR	XY_SUB
				INX
				INX
				INX
                JMP DRAW_MARKER
; On entry:
;	X: CURSOR
;
XY_SUB			LDY	:YPNTS,X
				LDA	#$B9
				CPX	#4
				BCC	:EXIT
				LDA	#$A7
:EXIT			TAX
				RTS

:YPNTS			DB	$7D,$86,$8F
				DB	$98,$A2,$AB
				DB	$B5

;-------------------------------------------------------------------------------

;*** TEST WITH CONSECUTIVE ROBOTS? ***

;*** Clear entire character area ***
;	PictClear 0;0;23;192

;*** ALSO, CHANGE BETWEEN DENEB AND NOT ***

OLD_PROFRACE	=	LOCAL_TEMP+0		;*** MAYBE NOT TEMP ***

CHECK_PROFRACE	LDA #$00
				STA OLD_PROFRACE
				JSR :CHECK_SUB
				LDA ITARGET_CHAR
				JSR SET_CHAR
				JSR :CHECK_SUB
				LDA ISOURCE_CHAR
				JMP SET_CHAR

:CHECK_SUB		LDY #profrace
				LDA (CHARDL),Y
				AND #$8F
				CMP #deneb
				BNE :1
				ORA #$40
:1				AND #$C0
				EOR OLD_PROFRACE
				STA OLD_PROFRACE
				RTS

DO_NEXT_CHAR	LDX ISOURCE_CHAR
				INX
				CPX GROUP_SIZE
				BNE :1
				LDX #0
:1				STX ITARGET_CHAR
				CPX ISOURCE_CHAR
				BEQ :99

				JSR CHECK_PROFRACE
				LDA OLD_PROFRACE
				; BMI :FULL_REDRAW

				JSR CLEAR_ITEMS

				; if D/ND changed
					; clear hand labels

				LDA ITARGET_CHAR
				STA ISOURCE_CHAR
				JSR SET_CHAR

				; if D/ND changed
					; redraw hand labels
				JSR DRAW_ITEMS

				JSR REDRAW_INFO
				JSR SCROLL_INFO_LEFT

				;*** POSSIBLY UPDATE FULL STATS ***
				;*** UPDATE EQUIPMENT ***
				;*** POSSIBLY CHANGE CHAR/ROBOT GRAPHIC ***

:99				RTS

DO_PREV_CHAR	LDX ISOURCE_CHAR
				BNE :1
				LDX GROUP_SIZE
:1				DEX
				STX ITARGET_CHAR
				CPX ISOURCE_CHAR
				BEQ :99

				; LDY #profrace
				; LDA (CHARDL),Y
				; STA OLD_PROFRACE

				JSR CLEAR_ITEMS
				;*** MAYBE CLEAR FULL STATS ***

				LDA ITARGET_CHAR
				STA ISOURCE_CHAR
				JSR SET_CHAR
				JSR REDRAW_INFO
				JSR SCROLL_INFO_RIGHT
				JSR DRAW_ITEMS
				;*** POSSIBLY UPDATE FULL STATS ***
				;*** UPDATE EQUIPMENT ***
				;*** POSSIBLY CHANGE CHAR/ROBOT GRAPHIC ***

:99				RTS

CLEAR_ITEMS		JSR IS_ROBOT
				BCS	:1
				JMP CLEAR_CHAR_ITEMS
:1				JMP CLEAR_ROBO_ITEMS

DRAW_ITEMS		JSR IS_ROBOT
				BCS	:1
				JMP DRAW_CHAR_ITEMS
:1				JMP DRAW_ROBO_ITEMS

REDRAW_INFO		JSR DRAW_PICT
				PictSetPage1
				PictClear 29;4;39;47
				PictCall DRAW_CHAR_VALS
				PictSetPage2
				PictEnd
				RTS

; CLEAR_IND_STATS	JSR DRAW_PICT
; 				PictClear 31;54;39;88		; status/shield
; 				PictClear 26;104;38;111		; energy
; 				PictClear 36;118;38;170		; 1 byte stats
; 				PictEnd
; 				RTS

FROM_Y			=	LOCAL_TEMP+0

SCROLL_INFO_LEFT
				LDY #29
:1				STY FROM_Y

				LDX #4
:2				LDA LOBYTES,X
				STA SCREENL
				STA DESTINL
				LDA HIBYTES,X
				STA SCREENH
				EOR #$60
				STA DESTINH

				LDY #30
:3				LDA (SCREENL),Y
				DEY
				STA (SCREENL),Y
				INY
				INY
				CPY #39
				BNE :3

				LDY FROM_Y
				LDA (DESTINL),Y
				LDY #38
				STA (SCREENL),Y
				INX
				CPX #47
				BNE :2

				LDY FROM_Y
				INY
				CPY #39
				BNE :1
				RTS

SCROLL_INFO_RIGHT
				LDY #38
:1				STY FROM_Y

				LDX #4
:2				LDA LOBYTES,X
				STA SCREENL
				STA DESTINL
				LDA HIBYTES,X
				STA SCREENH
				EOR #$60
				STA DESTINH

				LDY #37
:3				LDA (SCREENL),Y
				INY
				STA (SCREENL),Y
				DEY
				DEY
				CPY #28
				BNE :3

				LDY FROM_Y
				LDA (DESTINL),Y
				LDY #29
				STA (SCREENL),Y
				INX
				CPX #47
				BNE :2

				LDY FROM_Y
				DEY
				CPY #28
				BNE :1
				RTS

;-------------------------------------------------------------------------------

DRAW_CHAR_EQUIP	JSR	DRAW_PICT
				PictTextAt 42;0;(HEAD)
				PictTextAt 50;9;(:)
				PictTextAt 50;17;(:)
				PictTextAt 42;33;(BACK)
				PictTextAt 42;98;(HANDS)
				PictTextAt 42;147;(ARMOR)
				PictTextAt 50;156;(:)
				PictTextAt 42;172;(FEET)
				PictTextAt 50;181;(:)
				PictEnd

				; draw variable number of back slots
				LDY	#back
				LDA	(CHARDL),Y
				BEQ :2
				TAX
:1				STX :TEXT_MOD+1
				LDY CEQUIP_LINES+6-1,X
				LDX #38+6
				JSR MOVE_TO
				JSR DRAW_PICT
:TEXT_MOD		PictText (1:)
				PictEnd
				LDX :TEXT_MOD+1
				DEX
				BNE :1
:2
				LDY	#profrace
				LDA	(CHARDL),Y
				AND	#$0F
				CMP	#deneb
				BEQ	:DENEB

				JSR	DRAW_PICT
				PictTextAt 38;107;(_L:)
				PictTextAt 38;115;(_R:)
				PictEnd
				JMP DRAW_CHAR_ITEMS

:DENEB			JSR	DRAW_PICT
				PictSetTextHeight 8
				PictTextAt 38;107;(UL:\nUR:\nLL:\nLR:)
				PictSetTextHeight 0
				PictEnd
				; fall through

DRAW_CHAR_ITEMS	LDX #0
:1				STX COUNT
				TXA
				JSR	EQUIP_IND_TO_TEXT
				LDX COUNT
				LDY CEQUIP_LINES,X
				LDX #66
				JSR DRAW_TEXTBUF_XY
				LDX COUNT
				INX
				CPX #14
				BNE :1
				RTS

CLEAR_CHAR_ITEMS
				LDX #0
:1				STX COUNT
				LDA CEQUIP_LINES,X
				STA :MODY1
				CLC
				ADC #7
				STA :MODY2
				JSR	DRAW_PICT
				DB OpClear
				DB 9
:MODY1			DB $FF
				DB 23
:MODY2			DB $FF
				PictEnd
				LDX COUNT
				INX
				CPX #14
				BNE :1
				RTS

CEQUIP_LINES	DB	$09,$11,$6B
				DB	$73,$7B,$83
				DB	$2A,$32,$3A
				DB	$42,$4A,$52
				DB	$9C,$B5

DRAW_CHAR_INFO	JSR DRAW_PICT
				PictTextAt 171;4;(NAME:\nRACE:\nPROF:\nLEVL:\nEXPR:)
				PictEnd
				; fall through

DRAW_CHAR_VALS	JSR DRAW_PICT
				PictMoveTo $CC;4
				PictEnd

				JSR CNAME_TO_TEXT
				JSR DRAW_TEXTBUF_RET

				JSR CRACE_TO_TEXT
				JSR DRAW_TEXTBUF_RET

				JSR CPROF_TO_TEXT
				JSR DRAW_TEXTBUF_RET

				JSR CLEVEL_TO_TEXT
				JSR DRAW_TEXTBUF_RET

				JSR CEXP_TO_TEXT
				JMP DRAW_TEXTBUF_RET

DRAW_ROBO_EQUIP	LDX #9
				LDY	#e1_HEAD
				LDA	(CHARDL),Y
				BPL	:NOWITH
				JSR DRAW_PICT
				PictTextAt 26;9;(WITH:)
				PictEnd
				LDX #0
:NOWITH			STX :VMOVE_MOD+2
				JSR DRAW_PICT
:VMOVE_MOD		PictTextAt 26;9;(HEAD:)
				PictEnd
				JSR DRAW_PICT
				PictTextAt 42;33;(ARMS)
				;*** could use line breaks (with line height 8) ***
				PictTextAt 44;42;(L:)
				PictTextAt 44;50;(_:)
				PictTextAt 44;58;(R:)
				PictTextAt 44;66;(_:)
				PictTextAt 42;104;(BACK)
				PictTextAt 50;113;(:)
				PictTextAt 42;128;(PROGRAM)
				PictTextAt 50;137;(:)
				PictTextAt 42;152;(MOVEMENT_TYPE)
				PictTextAt 50;161;(:)
				PictEnd
				; fall through

DRAW_ROBO_ITEMS LDX #9
				LDY	#e1_HEAD
				LDA	(CHARDL),Y
				BPL :NOWITH
				LDX #0
:NOWITH			STX :VMOVE_MOD+2
				AND	#%00000111
				STA :RANGE_MOD
				JSR DRAW_PICT
				PictHMoveTo 26
:VMOVE_MOD		PictTextAt 66;9;(<SIGHT_RANGE-)-
:RANGE_MOD		USR	(_>)
				PictEnd

				LDX #0
:1				STX COUNT
				LDY :IND_TABLE,X
				JSR	CEQUIP_TO_TEXT
				LDX COUNT
				LDY REQUIP_LINES,X
				LDX #66
				JSR DRAW_TEXTBUF_XY
				LDX COUNT
				INX
				CPX #7
				BNE :1

				LDY	#e1_HEAD
				LDA	(CHARDL),Y
				AND	#%00011000
				LSR
				LSR
				LSR
				LDX #<RTREAD_NAMES
				LDY #>RTREAD_NAMES
				JSR GET_IND_STRING
				LDX #66
				LDY REQUIP_LINES+7
				JMP DRAW_TEXTBUF_XY

:IND_TABLE		DB	e2_SLOT
				DB	e3_UPLFT
				DB	e4_UPRGT
				DB	e5_LWLFT
				DB	e6_LWRGT
				DB	e7_BACK1
				DB	e15_PROGRAM

CLEAR_ROBO_ITEMS
				LDX #0
:1				STX COUNT
				LDA REQUIP_LINES,X
				STA :MODY1
				CLC
				ADC #7
				STA :MODY2
				JSR	DRAW_PICT
				DB OpClear
				DB 9
:MODY1			DB $FF
				DB 23
:MODY2			DB $FF
				PictEnd
				LDX COUNT
				INX
				CPX #8
				BNE :1
				RTS

REQUIP_LINES	DB	9
				DB	42
				DB	50
				DB	58
				DB	64
				DB	113
				DB	137
				DB  161					; movement type

RTREAD_NAMES	USR(TREAD)=
				USR(LEVITATOR)=
				USR(TETRA-LEG)=
				USR(BI-LEG)=

DRAW_ROBO_INFO	JSR DRAW_PICT
				PictTextAt 171;4;(NAME:\nPROF:\nLEVL:\nOWNR:)
				PictEnd
				; fall through

DRAW_ROBO_VALS	JSR DRAW_PICT
				PictMoveTo $CC;4
				PictEnd

				JSR CNAME_TO_TEXT
				JSR DRAW_TEXTBUF_RET

				JSR CPROF_TO_TEXT
				JSR DRAW_TEXTBUF_RET

				LDX #0
				LDY	#e15_PROGRAM+1
				LDA	(CHARDL),Y
				AND	#$08
				BNE	:1
				JSR CLEVEL_TO_TEXT
				LDY	#e15_PROGRAM+1
				LDA	(CHARDL),Y
				BPL	:2
				LDA	#$0B
				STA	TEXT_BUFFER,X
				INX
:1				LDA	#TextLineEnd
				STA	TEXT_BUFFER,X
:2				JSR DRAW_TEXTBUF_RET

				; find the owner cybernate linked to this robot
				;*** move this to CHARACTER.S?
				LDY	#robotcyblink
				LDA	(CHARDL),Y
				PHA
				LDX #0
:3				TXA
				JSR	SET_CHAR
				PLA
				PHA
				LDY	#disklocation
				CMP	(CHARDL),Y
				BEQ	:4
				INX
				BNE	:3
:4				PLA
				JSR CNAME_TO_TEXT
				JSR DRAW_TEXTBUF_RET

				LDA	ISOURCE_CHAR
				JMP	SET_CHAR

; TODO: put these in $D000 banks?

CHAR_IMAGE      DB  $00,$05,$06,$BB
                HEX 80008940607058585C955858707060604000A54060709701
                HEX 0556AD5C95588D7089608D40AD60970004585C9997003AA7
                HEX 0142608D70A1B70140009D97016870A59F017700CD408D00
                HEX 60382E2B2ABD2B2B2E2C8D2E892B2B2A6A3A1A0E06060395
                HEX 06060E0E06950E891E1A893A953B896B952B892AC92B992A
                HEX C12BCD2ABD2B9D2EA12BA57F7F55F97F4100FDA101890389
                HEX 07890D8D079D7D55FDFDA175A9559D7F010F7A6A2AC57E3A
                HEX 6A912A8D2B2E383830306091309160A5308D3889309D388D
                HEX 2CA19F062340897F2AED6A8D3A1A890E8D1A950E06910399
                HEX 01A900A90103061A7A2A897F9706AF070D1D1D3535758D35
                HEX 8D758D3F00910103071D357555ED97080CA11DA10DB5079D
                HEX 0DAD0F078D97079900FDDD017F55557F00FD890103970236
                HEX 1AA90E8D97079297087DF989DC00FDA9010303
                DB	$FF

ROBOT_IMAGE     DB  $00,$1F,$09,$A0
                HEX 8000D5209960957078588D7838382C3E6E2E997E2C3830AD
                HEX 78189B78AF0044308D604000FD99406000E500899F005C70
                HEX 307C2C2C7C97030A30997030009160715F5E565755897535
                HEX 1D1F1D1D358D757F757555AD7F0073182C1873007FAF0244
                HEX 55957F060C183060ED302C2B2B7F037F00E5301C0F070307
                HEX 0E1B077F00070D0D0797050A0091601C1B18897830307F2A
                HEX 957F0100B5418D43737B7E893E2A7F405F55895F407F2A2F
                HEX 2C2D912C2F2A959703857F0A22282A970464A704649F056C
                HEX 6A5A5655917F008B12891000121210100212101012021010
                HEX 9704880210001000A1079702604000081838709703030106
                HEX 1C6C0C0C0F06067F559557565C58706040607070784C4C7E
                HEX 5797022755577B0101407E55E99F045E51441555970664A7
                HEX 06649F076C575A6A2A919704834989400941490049410940
                HEX 49019F0689084149000000B50103060C1818198918951B1C
                HEX 970606060E0E1A1A3A3A6A8D2A2B2A892B3E6A7F2A957E03
                HEX 00709704577A1A1A7A1A1A9F08459F09486A957F6030180C
                HEX 0706E5070D3555557F407F00E500FDB10101030307070D0D
                HEX 1D1F3F358D057F01007C07358D3F0303010303019F0A469F
                HEX 0A4C9D00FD890189030700E5EC008D403F60601F0099FEB6
                HEX EE1C1E07677F7E7D00FEC4EE9705000100FEC6
                DB	$FF

;-------------------------------------------------------------------------------

; used by I.COMPRESS, I.REGENERATE, I.TRANSFER, and I.USE

BIG_STATS_BOX	LDA #$42
				STA SELECT

				JSR DRAW_PICT
                PictClear 24;50;39;51
				PictSetPage1
				PictClearPage
				PictRect $01;$00;$115;$4F
				PictMoveTo $01;$0A
				PictLineTo $115;$0A
				PictVMoveTo 2
				PictCall STATS_HEADER
                PictVMove 10
				PictEnd

                JSR FIRST_CHAR
:1              JSR STATS_LINE
                JSR DRAW_PICT
                PictVMove 8
                PictEnd
                JSR NEXT_CHAR
                BCC :1

				JSR DRAW_PICT
				PictTextAt 16;70;(CANCEL)
				PictSetPage2
				PictSwap $00;$00;$28;$50
				PictMoveTo $AB;$68
				PictLineTo $10F;$68
				PictEnd

				JSR RESET_CHRLIST
				LDA	#$00
				STA	LEGALS+7
				RTS

HIDE_BIG_STATS  JSR DRAW_PICT
                PictClear 24;80;39;105
                PictSwap $00;$50;$28;$00
                PictEnd
                RTS

;-------------------------------------------------------------------------------
